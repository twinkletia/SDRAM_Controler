#include "SDRAM_CTR.inc"
#include "ASYNCRX.inc"

#define READ		1'b1
#define WRITE		1'b0

#define tRP 		15'd3	// PREからACT
#define tRC 		15'd10	// REFからREF
#define tRCD		15'd3	// ACTからRWコマンド
#define tRAS		15'd7 	// ACTからPRE
#define tDAL		15'd5 	// WTからPRE終了
#define tCAC		15'd3	// CASレーテンシ

#define INIT_WAIT	15'd10000
#define INIT_PALL	INIT_WAIT+15'd1
#define INIT_REF1	INIT_PALL+tRP
#define INIT_REF2	INIT_REF1+tRC
#define INIT_REF3	INIT_REF2+tRC
#define INIT_REF4	INIT_REF3+tRC
#define INIT_REF5	INIT_REF4+tRC
#define INIT_REF6	INIT_REF5+tRC
#define INIT_REF7	INIT_REF6+tRC
#define INIT_REF8	INIT_REF7+tRC
#define INIT_MRS	INIT_REF8+tRC

#define PRECHARGE	13'b0010000000000

#define REF_CYC		9'd511	// リフレッシュサイクル

#define REF 		15'd0
#define ENDREF		REF+tRC

#define R_ACT		15'd0
#define R_EXE		R_ACT+tRCD
#define R_ENDPRE	R_ACT+tRAS+tRP
#define R_OUT		R_EXE+tCAC

#define W_ACT		15'd0
#define W_EXE 		W_ACT+tRCD
#define W_ENDPRE	W_EXE+tDAL

#define M_OPTION	6'b000000	// オプション [2] = Write burst mode
#define M_CASL		3'b010		// CASレーテンシ
#define M_WRAP		1'b0		// ラップタイプ
#define M_BURST		3'b011		// バースト長

module SDRAM_CTR{
	ASYNCRX rx;

	reg init_end = 0;

	reg cnt[15] = 0;
	reg ref_cnt[9] = 0;

	reg rdata0[16];
	reg rdata1[16];
	reg rdata2[16];
	reg rdata3[16];
	reg rdata4[16];
	reg rdata5[16];
	reg rdata6[16];
	reg rdata7[16];
	reg wdata0[16];
	reg wdata1[16];
	reg wdata2[16];
	reg wdata3[16];
	reg wdata4[16];
	reg wdata5[16];
	reg wdata6[16];
	reg wdata7[16];
	reg ldqm = 1;
	reg udqm = 1;

	reg ref_req = 0;	// リフレッシュリクエスト
	reg write_req = 0;	// 書込みリクエスト
	reg read_req = 0;	// 読込みリクエスト

	reg init_flag = 0;

	func_self rburst_terminate;
	func_self wburst_terminate;
	func_self rburst;
	func_self wburst;

	proc_name init();
	proc_name idle();
	proc_name refresh();
	proc_name read();
	proc_name write();

	func_self cmd_nop;	// 何もしない
	func_self cmd_pall;	// 全バンクプリチャージ
	func_self cmd_ref;	// オートリフレッシュ
	func_self cmd_mrs;	// モードレジスタ設定
	func_self cmd_act;	// バンクとROWをアクティブ
	func_self cmd_rda;	// AP付READ
	func_self cmd_wta;	// AP付WRITE

	DRAM_CKE  = 1;			// 常にHIGH
	DRAM_UDQM = udqm;		// 初期化中のみHIGH
	DRAM_LDQM = ldqm;		// 初期化中のみHIGH

	// モジュール間IF
	rx.aRxIn = rxin;
	rxout = rx.aRxOut;

	// データ受信要求
	func rx.aRxReq any{
		rw_mode == READ:  read_req  := 1;	
		rw_mode == WRITE: 	{
								write_req := 1;
								wdata0 := datai0;
								wdata1 := datai1;
								wdata2 := datai2;
								wdata3 := datai3;
								wdata4 := datai4;
								wdata5 := datai5;
								wdata6 := datai6;
								wdata7 := datai7;
							}
	}

	if(!init_flag){
		init();
		init_flag := 1;
	}

	// SDRAM初期化
	finish_init = init_end;
	proc init{
		if(cnt == INIT_MRS) cnt := 0;
		else				cnt++;

		alt{
			/*
				初期化のため、nopで最低100us待つ必要がある。(INIT_WAIT)
				待ち終わったら、全バンクをアイドル状態にするために、
				全バンクプリチャージをする。
			*/
			cnt <= INIT_WAIT: cmd_nop();
			cnt == INIT_PALL: cmd_pall();
			/*
				初期化後、最低8回オートリフレッシュを行う。
				終了次第、モードレジスタを使用することができる。
			*/
			cnt <  INIT_REF1: cmd_nop();
			cnt == INIT_REF1: cmd_ref();
			cnt <  INIT_REF2: cmd_nop();
			cnt == INIT_REF2: cmd_ref();
			cnt <  INIT_REF3: cmd_nop();
			cnt == INIT_REF3: cmd_ref();
			cnt <  INIT_REF4: cmd_nop();
			cnt == INIT_REF4: cmd_ref();
			cnt <  INIT_REF5: cmd_nop();
			cnt == INIT_REF5: cmd_ref();
			cnt <  INIT_REF6: cmd_nop();
			cnt == INIT_REF6: cmd_ref();
			cnt <  INIT_REF7: cmd_nop();
			cnt == INIT_REF7: cmd_ref();
			cnt <  INIT_REF8: cmd_nop();
			cnt == INIT_REF8: cmd_ref();
			cnt <  INIT_MRS : cmd_nop();
			/*
				モードレジスタをセットし、初期化処理を終える。
			*/
			cnt == INIT_MRS :{
				cmd_mrs();
				ldqm := 0;
				udqm := 0;
				ref_cnt := REF_CYC;
				init_end := 1;
				idle();
			}
		}
	}

	proc idle{
		cmd_nop();

		alt{
			// リフレッシュ最優先
			ref_req:{
				refresh();
				ref_req := 0;
			}
			write_req:{
				write();
				write_req := 0;
			}
			read_req:{
				read();
				read_req := 0;
			}
		}
	}

	// REF_CYCクロック毎にオートリフレッシュ
	proc refresh{
		if(cnt == ENDREF-15'd2) cnt := 0;
		else				   cnt++;

		alt{
			cnt == REF:			cmd_ref();
			cnt <  ENDREF-15'd2:	cmd_nop();
			cnt == ENDREF-15'd2:{
								cmd_nop();
								ref_cnt := REF_CYC;
								idle();
			}
		}
	}

	// リフレッシュカウンタ
	// (REF_CYC+1)クロック目にリフレッシュリクエストを送る
	if(ref_cnt != 0){
		if(ref_cnt == 1) ref_req := 1;
		ref_cnt--;
	}

	datao0 = rdata0;
	datao1 = rdata1;
	datao2 = rdata2;
	datao3 = rdata3;
	datao4 = rdata4;
	datao5 = rdata5;
	datao6 = rdata6;
	datao7 = rdata7;

	proc read{
		if(cnt == R_ENDPRE-15'd2) cnt := 0;
		else					 cnt++;

		alt{
			cnt == R_ACT:		cmd_act();
			cnt <  R_EXE:		cmd_nop();
			cnt == R_EXE:		cmd_rda();
			cnt <  R_OUT:		cmd_nop();
			cnt == R_OUT:{
								cmd_nop();
								rburst_terminate();
								rx.aRxAck();
			}
			cnt <  R_ENDPRE-15'd2:	cmd_nop();
			cnt == R_ENDPRE-15'd2:{
									cmd_nop();
									idle();
			}
		}
	}

	proc write{
		if(cnt == W_ENDPRE-15'd2) cnt := 0;
		else					 cnt++;

		alt{
			cnt == W_ACT:		cmd_act();
			cnt <  W_EXE:		cmd_nop();
			cnt == W_EXE:{
								cmd_wta();
								wburst_terminate();
								rx.aRxAck();
			}
			cnt <  W_ENDPRE-15'd2:	cmd_nop();
			cnt == W_ENDPRE-15'd2:{
									cmd_nop();
									idle();
			}
		}
	}

	func wburst{
		DRAM_DO = wdata0;
		wdata0 := wdata1;
		wdata1 := wdata2;
		wdata2 := wdata3;
		wdata3 := wdata4;
		wdata4 := wdata5;
		wdata5 := wdata6;
		wdata6 := wdata7;
	}

	func wburst_terminate seq{
		wburst();
		wburst();
		wburst();
		wburst();
		wburst();
		wburst();
		wburst();
		wburst();
	}

	func rburst_terminate seq{
		rburst();
		rburst();
		rburst();
		rburst();
		rburst();
		rburst();
		rburst();
		rburst();
	}

	func rburst{
		rdata7 := DRAM_DI;
		rdata6 := rdata7;
		rdata5 := rdata6;
		rdata4 := rdata5;
		rdata3 := rdata4;
		rdata2 := rdata3;
		rdata1 := rdata2;
		rdata0 := rdata1;
	}

	func cmd_nop{
		DRAM_CS  = 0;
		DRAM_RAS = 1;
		DRAM_CAS = 1;
		DRAM_WE  = 1;
	}

	func cmd_pall{
		DRAM_CS  = 0;
		DRAM_RAS = 0;
		DRAM_CAS = 1;
		DRAM_WE  = 0;

		DRAM_A = PRECHARGE;
	}

	func cmd_ref{
		DRAM_CS  = 0;
		DRAM_RAS = 0;
		DRAM_CAS = 0;
		DRAM_WE  = 1;
	}

	func cmd_mrs{
		DRAM_CS  = 0;
		DRAM_RAS = 0;
		DRAM_CAS = 0;
		DRAM_WE  = 0;

		DRAM_BA = 2'b00;
		DRAM_A  = {M_OPTION, M_CASL, M_WRAP, M_BURST};
	}

	func cmd_act{
		DRAM_CS  = 0;
		DRAM_RAS = 0;
		DRAM_CAS = 1;
		DRAM_WE  = 1;

		DRAM_BA = bank;
		DRAM_A  = row;
	}

	func cmd_rda{
		DRAM_CS  = 0;
		DRAM_RAS = 1;
		DRAM_CAS = 0;
		DRAM_WE  = 1;

		DRAM_BA = bank;
		DRAM_A  = 13'(column) | PRECHARGE;
	}

	func cmd_wta{
		DRAM_CS  = 0;
		DRAM_RAS = 1;
		DRAM_CAS = 0;
		DRAM_WE  = 0;

		DRAM_BA = bank;
		DRAM_A  = 13'(column) | PRECHARGE;
	}
}
